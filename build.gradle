plugins {
	id 'idea'
	id 'java'
	id 'distribution'
	id 'application'
	id 'maven-publish'
	id 'com.palantir.git-version' version '0.15.0'
}

repositories {
	mavenCentral()
}

group 'org.dougmcintosh'
version gitVersion()

dependencies {
	implementation "org.apache.tika:tika-parsers:${tikaVersion}"
	implementation "org.apache.lucene:lucene-core:${luceneVersion}"
	implementation "org.apache.lucene:lucene-analyzers-common:${luceneVersion}"
	implementation "org.apache.lucene:lucene-highlighter:${luceneVersion}"
	implementation "org.apache.lucene:lucene-queryparser:${luceneVersion}"
	implementation "commons-cli:commons-cli:${commonsCliVersion}"
	implementation "org.apache.logging.log4j:log4j-api:${log4j2Version}"
	implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j2Version}"
	implementation "org.slf4j:slf4j-api:${slf4jVersion}"
	implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
	implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
	implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
	implementation "com.google.guava:guava:${guavaVersion}"
}

plugins.withType(DistributionPlugin) {
	distTar {
		compression = Compression.GZIP
		archiveExtension = 'tgz'
	}
}

distributions {
	main {
		distributionBaseName = 'mcintosh-indexer'
		contents {
			from('var/conf') {
				into 'var/conf'
			}
		}
	}
}

application {
	mainClass = "org.dougmcintosh.index.IndexerCli"
	applicationDefaultJvmArgs = ['-Xmx4g']
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(16)
	}
}

publishing {
	publications {
		defaultPkg(MavenPublication) {
			from components.java
		}
	}
}